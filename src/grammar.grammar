@external tokens endToken from "./tokens.js" { end }

@external propSource highlight from "./highlight"

@precedence { postfix @left, prefix, times @left, plus @left, cmp @left, eq @left, and @left, or @left, set @right, block @cut }

@top Program { stml* }

kw<word> { @specialize[@name={word}]<identifier, word> }

list<item> { item ("،" item)* "،"? }

doubleList<item1, item2> { item1 ("،" item1)* ("،" item2 ("،" item2)*)? "،"? }

VarDef { identifier }

ObjectDes { "{" list<((PropName DefOp<":"> definable) | VarDef) (DefOp<"="> expr)?>? "}" } 

ListDes { "[" list<definable>? "]" }

definable {
  VarDef |
  ObjectDes |
  ListDes
}

varDeclTail { DefOp<"="> expr }

singleVarDecl { VarDef (varDeclTail)? }

desVarDecl { (ObjectDes | ListDes) varDeclTail }

VarDecl { kw<"متغير"> (singleVarDecl | desVarDecl) ("،" (singleVarDecl | desVarDecl))* end }

param { definable }

optionalParam { definable DefOp<"="> expr }

Params { "(" (doubleList<param, optionalParam> | list<optionalParam>)? ")" }

FuncDecl { kw<"دالة"> VarDef Params Block }

Block { !block "{" stml* "}" }

stml {
  VarDecl | FuncDecl |
  
  ImportDecl { kw<"استورد"> definable kw<"من"> String } |

  ExportDecl { kw<"صدّر"> (VarDecl | FuncDecl) } |

  IfElseStml { kw<"إن"> "(" expr ")" Block (kw<"وإن"> "(" expr ")" Block)* (kw<"إلا"> Block)? } |
  
  WhileStml { kw<"طالما"> "(" expr ")" Block } |

  LoopStml { kw<"كرر"> Block } |

  ForIn { kw<"لكل"> "(" definable kw<"في"> expr ")" Block } |

  TryCatchStml { kw<"حاول"> Block kw<"أمسك"> "(" VarDef ")" Block } |

  ReturnStml { kw<"أرجع"> expr? end } |

  ThrowStml { kw<"ألقي"> expr? end } |

  BreakStml { kw<"إكسر"> } |

  ContinueStml { kw<"واصل"> } |

  Block |

  expr end
}

VarName { identifier }

PropDef { identifier }

PropName { identifier }

minus[@dynamicPrecedence=1] { ArithOp<"-"> }

neg { ArithOp<"-"> }

binaryExprTail {
  !plus (ArithOp<"+"> | minus) expr |
  !times ArithOp<"*" | "/" | "%"> expr |
  !cmp CmpOp<">" | ">=" | "<" | "<="> expr |
  !eq CmpOp<"==" | "!="> expr |
  !and LogicOp<"&&"> expr |
  !or LogicOp<"||"> expr
}

LambdaParams {
  "|" list<param>? "|"
}

expr {
  VarName ~ambig |

  String | Number |

  Bool { @specialize<identifier, "صحيح" | "خطأ"> } |

  kw<"عدم"> |

  Object { "{" list<PropDef ~ambig (DefOp<":"> expr)?>? "}" ~ambig } |

  List { "[" list<expr>? "]" ~ambig } |

  UnaryExpr { !prefix (LogicOp<"!"> | neg) expr } |

  BinaryExpr {
    expr binaryExprTail
  } |

  CallExpr { expr !postfix Args } |

  MemberExpr ~ambig |
  
  AssignExpr |

  Lambda { LambdaParams Block } |

  Group { "(" expr ")" }
}

Args { "(" list<expr>? ")" } 

MemberExpr { expr !postfix (("[" expr "]") | ("." PropName)) }

assignable {
  VarName ~ambig |
  MemberExpr ~ambig |
  ObjectDes { "{" list<((PropName ~ambig UpdateOp<":"> assignable) | VarName ~ambig) (UpdateOp<"="> expr)?>? "}" ~ambig } |
  ListDes { "[" list<assignable>? "]" ~ambig }
}

AssignExpr {
  assignable !set UpdateOp<"="> expr |
  (VarName | MemberExpr) !set UpdateOp<"*=" | "/=" | "%=" | "+=" | "-="> expr
}

@skip { space | newline | Comment }

UpdateOp<op> { op }

DefOp<op> { op }

@tokens {
  Comment { "#" ![\n]* }

  space { $[\u0009\u000b\u000c\u0020\u0085\u200e\u200f] }

  newline { $[\u000a\u000d\u2028\u2029] }

  identifierChar { (std.asciiLetter | $[_\u{0621}-\u{063a}] | $[\u{0641}-\u{0652}])+ }

  identifier { identifierChar (identifierChar | std.digit)* }

  @precedence { space, newline, identifier }

  String { '"' (![\\\n"] | "\\" _)* '"'? }

  Number { std.digit+ ("." std.digit+)? }

  ArithOp<op> { op }

  CmpOp<op> { op }

  LogicOp<op> { op }

  "." "،" "|"

  "(" ")" "[" "]" "{" "}"
}
